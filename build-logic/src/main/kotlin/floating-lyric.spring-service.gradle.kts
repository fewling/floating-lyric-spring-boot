// Convention plugin for a runnable Spring Boot SERVICE.
// Apply with: id("floating-lyric.spring-service")
plugins {
	kotlin("jvm")
	kotlin("plugin.spring")
	id("org.springframework.boot")
	id("io.spring.dependency-management")
}

group = "com.floating.lyrics"
version = "0.0.1-SNAPSHOT"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	"implementation"("org.springframework.boot:spring-boot-starter")
	"implementation"("org.jetbrains.kotlin:kotlin-reflect")
	"testImplementation"("org.springframework.boot:spring-boot-starter-test")
	"testImplementation"("org.jetbrains.kotlin:kotlin-test-junit5")
	"testRuntimeOnly"("org.junit.platform:junit-platform-launcher")
}

kotlin {
	compilerOptions {
		freeCompilerArgs.addAll("-Xjsr305=strict", "-Xannotation-default-target=param-property")
	}
}

tasks.withType<Test> {
	useJUnitPlatform()
}
